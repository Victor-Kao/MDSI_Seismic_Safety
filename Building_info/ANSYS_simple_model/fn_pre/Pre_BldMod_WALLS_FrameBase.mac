!*************Geometry*************************!
/PREP7

!*********** Creating keypoints **************************!
*DO,i_storey,0,n_storeys,1
    *DO,i,0,n_roomsy,1
        *DO,j,0,n_roomsx,1
            n_kp=(i_storey*(n_roomsy+1)*(n_roomsx+1))+(i*(n_roomsx+1))+j+1
            K,n_kp,j*l,i*b,i_storey*h
        *ENDDO
    *ENDDO
*ENDDO		

!*********** Creating beams and columns **************************!
*DO,i,0,n_storeys,1
    *DO,j,0,n_roomsy,1
        *DO,k,1,n_roomsx,1
            start=(i*(n_roomsy+1)*(n_roomsx+1))+j*(n_roomsx+1)+k
            end=start+1
            LSTR,start,end
        *ENDDO
    *ENDDO

    *DO,j,1,n_roomsx+1,1
        *DO,k,0,n_roomsy-1,1
            start=(i*(n_roomsy+1)*(n_roomsx+1))+k*(n_roomsx+1)+j
            end=start+n_roomsx+1
            LSTR,start,end
        *ENDDO
    *ENDDO

    *IF,i,EQ,(n_storeys),*EXIT

    *DO,j,1,((n_roomsx+1)*(n_roomsy+1)),1
        start=(i*(n_roomsy+1)*(n_roomsx+1))+j
        end=start+(n_roomsx+1)*(n_roomsy+1)
        LSTR,start,end
    *ENDDO
*ENDDO
/PNUM,KP,0 			! Turn on keypoint numbering
/PNUM,Line,0 		! Turn on line numbering

!*********** Creating floor and walls **************************!
*DO,i,1,n_storeys,1

    *DO,j,1,n_roomsy,1
        *DO,k,1,n_roomsx,1
            p1=k+(i*(n_roomsy+1)*(n_roomsx+1))+(n_roomsx+1)*(j-1)
            p2=p1+1
            p3=k+1+(n_roomsx+1)*j+(i*(n_roomsy+1)*(n_roomsx+1))
            p4=p3-1
            A,p1,p2,p3,p4
        *ENDDO
    *ENDDO

    *IF,i,EQ,(n_storeys),*EXIT
*ENDDO  
*DO,i,0,n_storeys,1
    *IF,i,EQ,(n_storeys),*EXIT

    *DO,j,1,n_roomsx+1,1
        *DO,k,0,n_roomsy-1,1
            p1=((i)*(n_roomsy+1)*(n_roomsx+1))+k*(n_roomsx+1)+j
            p2=p1+n_roomsx+1
            p3=p2+((n_roomsx+1)*(n_roomsy+1))
            p4=p1+((n_roomsx+1)*(n_roomsy+1))
            A,p1,p2,p3,p4
        *ENDDO
    *ENDDO
*ENDDO    
/PNUM,Area,0

NUMMRG,ALL

n_floorbeams=((n_roomsx)*(n_roomsy+1))+((n_roomsx+1)*(n_roomsy))
n_floorcolumns=(n_roomsx+1)*(n_roomsy+1)
lines_storey=n_floorbeams+n_floorcolumns
!*************Material assignemnt*************************!
ET,1,BEAM189 				! Use BEAM189 elements
SECTYPE,1,BEAM,RECT 		! Use a rectangular cross section for beam
SECOFFSET,CENT 				! Offset beam node to the centroid
SECDATA,b_col,h_col, 		! Use 0.1 x 0.1 cross section
MP,EX,1,E_col				! Define Young's modulus for aluminum: material #1
MP,PRXY,1,nu_col 			! Define Poisson's ratio for aluminum: material #1
MP,DENS,1,rho_col,,,

LSEL,S,LINE,,(n_floorbeams+1),(lines_storey)
*DO,i,1,n_storeys-1,1
    LSEL,A,LINE,,(i*lines_storey+n_floorbeams+1),((i+1)*lines_storey)    
*ENDDO
LATT,1,0,1,,,,1				! Set element attributes: material #1, element #1

ET,2,BEAM189 				! Use BEAM189 elements
SECTYPE,2,BEAM,RECT 		! Use a rectangular cross section for beam
SECOFFSET,CENT 				! Offset beam node to the centroid
SECDATA,b_beam,h_beam, 		! Use 0.1 x 0.1 cross section
MP,EX,2,E_beam				! sDefine Young's modulus for aluminum: material #2
MP,PRXY,2,nu_beam 			! Define Poisson's ratio for aluminum: material #2
MP,DENS,2,rho_beam,,,

LSEL,S,TYPE,,1
LSEL,INVE
LATT,2,0,2,,,,2				! Set element attributes: material #2, element #2

ET,3,SHELL181 				! Use PLANE183 elements
SECTYPE,3,SHELL
SECDATA,t_floor
MP,EX,3,E_floor 			! Define Young's modulus for material #3
MP,PRXY,3,nu_floor 			! Define Poisson's ratio for material #3
MP,DENS,3,rho_floor,,,
!MP,DMPR,3,0.1
ASEL,S,LOC,Z,0,0
*DO,i,1,n_storeys,1
    ASEL,A,LOC,Z,i*h,i*h
*ENDDO
AATT,3,0,3,,3				! Set element attributes: material #3, element #3

ET,4,SHELL181 				! Use PLANE183 elements
SECTYPE,4,SHELL
SECDATA,t_walls
MP,EX,4,E_floor 			! Define Young's modulus for material #3
MP,PRXY,4,nu_floor 			! Define Poisson's ratio for material #3
MP,DENS,4,rho_floor,,,
!MP,DMPR,3,0.1
ASEL,S,LOC,Z,0,0
*DO,i,1,n_storeys,1
    ASEL,A,LOC,Z,i*h,i*h
*ENDDO
ASEL,INVE
AATT,4,0,4,,4

!**************************** Mesh generation ****************************!
LSEL,ALL
LSEL,S,MAT,,1
LESIZE,ALL,,,nVolz

LSEL,ALL 
LSEL,S,LINE,,1,((n_roomsx)*(n_roomsy+1))
*DO,i,1,n_storeys,1
    LSEL,A,LINE,,(i*lines_storey+1),(i*lines_storey+((n_roomsx)*(n_roomsy+1)))    
*ENDDO
LESIZE,ALL,,,nVolx

LSEL,ALL 
LSEL,S,LINE,,((n_roomsx)*(n_roomsy+1))+1,lines_storey
*DO,i,1,n_storeys,1
    LSEL,A,LINE,,(i*lines_storey+n_roomsx*(n_roomsy+1)+1),(i*lines_storey+n_floorbeams)    
*ENDDO
LESIZE,ALL,,,nVoly

LSEL,ALL 					! Select all lines in the model
LMESH,ALL					! Mesh all lines

ASEL,ALL 					! Select all areas in the model
MSHKEY,1
AMESH,ALL					! AMESH, NA1, NA2, NINC Mesh areas from NA1 to NA2 (defaults to NA1) in steps of NINC (defaults to 1)

!----Merge nodes at same position - coupling
NSEL,ALL
ASEL,ALL
NUMMRG,ALL
NUMMRG,KP
NUMMRG,NODES

!---Plot nodes and elements
nplot
/PNUM,NODE,0
/PNUM,ELEM,0
eplot



